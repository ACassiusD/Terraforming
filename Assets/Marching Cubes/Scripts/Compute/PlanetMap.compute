//Generate 3D density field for terrain
#pragma kernel CSMain
#include "/Includes/Noise.compute"

RWTexture3D<float> DensityTexture;
int textureSize;
float planetSize;
float noiseScale;
float noiseHeightMultiplier;

float fractalBrownianMotion(int numLayers, float lacunarity, float persistence, float scale, float3 pos)
{
	float noise = 0;
	float frequency = scale / 100;
	float amplitude = 1;

	for (int i = 0; i < numLayers; i ++) {
		float n = 1-abs(snoise(pos * frequency)*2-1);
		//n*=n;
		noise += n * amplitude;

		amplitude *= persistence;
		frequency *= lacunarity;
	}

	return noise;
}

// 
float calculateDensity(int3 gridPoint){
    float3 worldPos = (gridPoint / float(textureSize - 1.0) - 0.5f) * planetSize;

	float halfS = planetSize / 2;
	float maxD = length(float3(halfS,halfS,halfS));

	float fudge = 1;

	float density = length(worldPos) / (maxD + fudge) - 0.5;

	// Noise
    float noise = fractalBrownianMotion(6, 2, 0.5, noiseScale, worldPos) * noiseHeightMultiplier;

	density += noise;

	return density;
}

//id = Thread id || the unique 3d grid position in the texture.
[numthreads(8, 8, 8)] //Size of each individial thread Group  8×8×8=512 threads
void CSMain (int3 id : SV_DispatchThreadID)
{
	//If we are on a thread that is outside the texture, return.
	//Handling the extra threads created from rouding up for the thread groups.
	if (id.x >= textureSize || id.y >= textureSize || id.z >= textureSize) {
		return;
	}

	float density = 0;

	const int b = 1;
	if (id.x >= textureSize-b || id.y >= textureSize-b || id.z >= textureSize-b) {
		density = 1;
	}
	else if (id.x <= b || id.y <= b || id.z <=b) {
		density = 1;
	}
	else {
		density = calculateDensity(id);
	}

	DensityTexture[id] = density;
}
